plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Global exclude configurations for logging conflicts
configurations.all {
    exclude group: 'ch.qos.logback', module: 'logback-classic'
    exclude group: 'ch.qos.logback', module: 'logback-core'
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    exclude group: 'org.springframework', module: 'spring-jcl'
    exclude group: 'org.slf4j', module: 'slf4j-simple'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'commons-logging', module: 'commons-logging'
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    
    // Spring dependencies for context integration (compileOnly to avoid packaging conflicts)
    compileOnly 'org.springframework:spring-context:6.0.13'
    compileOnly 'org.springframework:spring-core:6.0.13'
    compileOnly 'org.springframework:spring-beans:6.0.13'
}

shadowJar {
    archiveBaseName = 'dsl-library'
    archiveVersion = ''
    archiveClassifier = ''
    
    // Enable zip64 for large archives
    zip64 = true
    
    // Exclude signature files
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

// Make shadowJar the default jar task
jar.dependsOn shadowJar
